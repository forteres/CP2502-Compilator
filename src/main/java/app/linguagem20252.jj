options {
    STATIC = false;
}

PARSER_BEGIN(Linguagem20252)
package app;

import java.io.InputStream;
import java.io.FileInputStream;
import java.io.FileNotFoundException;
import java.lang.reflect.Array;import java.util.ArrayList;
import java.util.List;
import java.util.Objects;
import static app.Linguagem20252Constants.*;
import org.javatuples.Pair;
import org.javatuples.Triplet;

public class Linguagem20252 {

    public static int errosSemanticosCount = 0;
    public static int errosSintaticosCount = 0;
    public static int errosLexicosCount = 0;
    public static StringBuilder errosSintaticos = new StringBuilder();
    public static StringBuilder errosSemanticos = new StringBuilder();
    private static AnalisadorSemantico analisadorSemantico = new AnalisadorSemantico();


    public String getListaTokensEsperados(int[][] conjuntoDeTokensEsperados){
        StringBuilder listaTokens = new StringBuilder();
        for (int[] conjunto : conjuntoDeTokensEsperados){
            for (int token : conjunto){
                String descricao = OutputDictionary.getCategory(token);

                if (descricao.isEmpty()) {
                    descricao = tokenImage[token];
                }
                listaTokens.append(descricao).append(" ");
            }
        }
        return listaTokens.toString().trim();
    }

    public static java.util.ArrayList analiseLexica(InputStream input) throws ParseException {
            errosLexicosCount = 0;
            Linguagem20252 lexer = new Linguagem20252(input);
            java.util.ArrayList tokens = new java.util.ArrayList();
            Token token;
            do {
                token = lexer.getNextToken();
                if(token.kind == Linguagem20252Constants.MALFORMED_IDENTIFIER
                    || token.kind == Linguagem20252Constants.MALFORMED_NUMBER
                    || token.kind == Linguagem20252Constants.MALFORMED_REAL
                    || token.kind == Linguagem20252Constants.INVALID_SIMBOL
                    || token.kind == Linguagem20252Constants.UNCLOSED_QUOTE
                    || token.kind == Linguagem20252Constants.UNCLOSED_STRING
                    || token.kind == Linguagem20252Constants.UNCLOSED_BLOCK_COMMENT){
                    tokens.add(token);
                    errosLexicosCount++;
                }
            } while (token.kind != Linguagem20252Constants.EOF);

            return tokens;
        }

    public static Pair<StringBuilder,StringBuilder> analise(InputStream input) throws ParseException {
            Linguagem20252 parser = new Linguagem20252(input);
            analisadorSemantico = new AnalisadorSemantico();
            errosSintaticosCount = 0;
            errosSemanticosCount = 0;
            errosSintaticos = new StringBuilder();
            errosSemanticos = new StringBuilder();
            parser.Programa();
            return new Pair<>(errosSintaticos, errosSemanticos);
    }

    public Token skipToSynchronizingToken(int... synchronizingKinds) {
        Token t = getToken(1);

        while (t.kind != EOF && !isSynchronizingToken(t, synchronizingKinds)) {
            t = getNextToken();
        }

        return getToken(1);
    }

    private static boolean isSynchronizingToken(Token t, int... synchronizingKinds) {
        for (int kind : synchronizingKinds) {
            if (t.kind == kind) return true;
        }
        return false;
    }

    boolean isEOF() {
        return getToken(1).kind == EOF;
    }

    private void executarAcaoSemantica(Runnable acao) {
        try {
            acao.run();
        } catch (IllegalArgumentException e) {
            errosSemanticosCount++;
            errosSemanticos.append("Erro semântico na linha " + getToken(1).beginLine + ", coluna " + getToken(1).beginColumn + ": " + e.getMessage() + "\n\n");
        } catch (Exception e) {
            errosSemanticosCount++;
            errosSemanticos.append("Erro semântico inesperado na linha " + getToken(1).beginLine + ", coluna " + getToken(1).beginColumn + ": " + e.getMessage() + "\n\n");
        }
    }

    public static ArrayList<Triplet<Integer, String, Object>> getCodigoIntermediario() {
            return analisadorSemantico.getListaDeInstrucoes();
    }
}

PARSER_END(Linguagem20252)



SKIP:
{
< BLANK_SPACE: " ">
| <END_OF_LINE: "\n">
| <NEW_LINE:"\r">
| <TABULATION:"\t">
}

SPECIAL_TOKEN :
{
    < SINGLE_LINE_COMMENT: "//" (~["\n","\r"])* ("\n"|"\r"|"\r\n")>
    | < BLOCK_COMMENT: "/*" (~["*"] | "*" (~["/"]))* "*/">
    
}

TOKEN: {
        // LITERAIS ------------------------------------------
         < MALFORMED_REAL: (["0"-"9"])((["0"-"9"])?){2}  "." ((["0"-"9"] ["a"-"z","A"-"Z","_"]) | (["a"-"z","A"-"Z","_"] (["0"-"9"])?) (["0"-"9"] | ["a"-"z","A"-"Z","_"])*
        | (["0"-"9"]){2} (["0"-"9"] | ["a"-"z","A"-"Z","_"])+)>
        | < REAL: (["0"-"9"])((["0"-"9"])?){2} "." (["0"-"9"])(["0"-"9"])?>
        | < MALFORMED_NUMBER: ((["0"-"9"]) ((["0"-"9"] ["a"-"z","A"-"Z","_"]) | (["a"-"z","A"-"Z","_"] ["0"-"9"] | ["a"-"z","A"-"Z","_"])) (["0"-"9"])*)
            | (["0"-"9"]){3} (["0"-"9"] | ["a"-"z","A"-"Z","_"])+>
        | < NUMBER: (["0"-"9"])((["0"-"9"])?){2}>
        | < TEXT_DOUBLE: "\"" ( (~["\"","\\","\n","\r"]) | ("\\" ~[]) )* "\"" >
        | < TEXT_SINGLE: "'"  ( (~["'","\\","\n","\r"])  | ("\\" ~[]) )* "'" >
        | < UNCLOSED_STRING: ("\"" | "\'") ( (~["\"", "'","\\","\n","\r"]) | ("\\" ~[]) )+ >
        | < UNCLOSED_QUOTE: ("\"" | "\'") >
        | < UNCLOSED_BLOCK_COMMENT: "/*" (~["*"] | ("*" ~["/"]))*  >
        // SIMBOLOS ESPECIAIS --------------------------------

        | < ASSIGN: "=" >
        | < EQUAL: "==" >
        | < NOT_EQUAL: "!=" >
        | < SEMICOLON: ";" >
        | < COLON: ":" >
        | < COMMA: "," >
        | < DOT: "." >
        | < LBRACE: "{" >
        | < RBRACE: "}" >
        | < LBRACKET: "[" >
        | < RBRACKET: "]" >
        | < LPAREN: "(" >
        | < RPAREN: ")" >
        | < PLUS: "+" >
        | < MINUS: "-" >
        | < STAR: "*" >
        | < SLASH: "/" >
        | < PERCENT: "%" >
        | < POWER: "**" >
        | < DOUBLE_PERCENT: "%%" >
        | < SHIFT_LEFT: "<<" >
        | < SHIFT_RIGHT: ">>" >
        | < SHIFT_LEFT_ASSIGN: "<<=" >
        | < SHIFT_RIGHT_ASSIGN: ">>=" >
        | < EXCLAMATION: "!" >
        | < PIPE: "|" >
        | < AMPERSAND: "&" >

        // TIPOS PRIMITIVOS ----------------------------------

        | < TYPE_NUM:  (("n"|"N")("u"|"U")("m"|"M")) >
        | < TYPE_REAL: (("r"|"R")("e"|"E")("a"|"A")("l"|"L")) >
        | < TYPE_TEXT: (("t"|"T")("e"|"E")("x"|"X")("t"|"T")) >
        | < TYPE_FLAG: (("f"|"F")("l"|"L")("a"|"A")("g"|"G")) >

        // PALAVRAS RESERVADAS -----------------------------------
        | < BEGIN:   (("b"|"B")("e"|"E")("g"|"G")("i"|"I")("n"|"N")) >
        | < DEFINE:  (("d"|"D")("e"|"E")("f"|"F")("i"|"I")("n"|"N")("e"|"E")) >
        | < START:   (("s"|"S")("t"|"T")("a"|"A")("r"|"R")("t"|"T")) >
        | < END:     (("e"|"E")("n"|"N")("d"|"D")) >
        | < SET:     (("s"|"S")("e"|"E")("t"|"T")) >
        | < READ:    (("r"|"R")("e"|"E")("a"|"A")("d"|"D")) >
        | < SHOW:    (("s"|"S")("h"|"H")("o"|"O")("w"|"W")) >
        | < IF:      (("i"|"I")("f"|"F")) >
        | < THEN:    (("t"|"T")("h"|"H")("e"|"E")("n"|"N")) >
        | < ELSE:    (("e"|"E")("l"|"L")("s"|"S")("e"|"E")) >
        | < LOOP:    (("l"|"L")("o"|"O")("o"|"O")("p"|"P")) >
        | < WHILE:   (("w"|"W")("h"|"H")("i"|"I")("l"|"L")("e"|"E")) >

        // FLAG --------------------------------------------------
        | < TRUE:  (("t"|"T")("r"|"R")("u"|"U")("e"|"E")) >
        | < FALSE: (("f"|"F")("a"|"A")("l"|"L")("s"|"S")("e"|"E")) >

        // IDENTIFICADOR -------------------------------------
        | < MALFORMED_IDENTIFIER:
            (["a"-"z","A"-"Z","_"]) (((["a"-"z","A"-"Z","_"])* (["0"-"9"](["0"-"9"])+) (["a"-"z","A"-"Z","_"])*)+
            | (((["a"-"z","A"-"Z","_"])* (["0"-"9"] ["a"-"z","A"-"Z","_"])*)* ["0"-"9"]))
        >
        | < IDENTIFIER: ["a"-"z","A"-"Z","_"] ((["a"-"z","A"-"Z","_"])* (["0"-"9"] ["a"-"z","A"-"Z","_"])*)* >

        // ERRO ----------------------------------------------

        | < INVALID_SIMBOL: ~[]>
}


// # PROGRAMA
void Programa() :
{}
{
    try{
        <BEGIN> NomeDoPrograma() BlocoDefine() BlocoStart() BlocoEnd()
    } catch (ParseException e){
        errosSintaticosCount++;
        String tokensEsperados = getListaTokensEsperados(e.expectedTokenSequences);

        TokenStringBuilder.formatErroSintaticoToString(e, errosSintaticos, tokensEsperados);

        if (tokensEsperados.contains("begin")){
            errosSintaticos.append("Erro sintático: Programa deve iniciar com 'BEGIN'").append("\n\n");
        }
        Token t = getToken(1);
        if (t.kind == EOF){
            errosSintaticos.append("Erro sintático: Fim inesperado do programa.").append("\n\n");
        }
        // RECUPERAÇÃO DE ERROS
        skipToSynchronizingToken(END, START, DEFINE);
        t = getToken(1);
        if (t.kind == END){
            try{BlocoEnd();} catch (ParseException f){ // melhorar
                errosSintaticosCount++;
                tokensEsperados = getListaTokensEsperados(e.expectedTokenSequences);
                TokenStringBuilder.formatErroSintaticoToString(e, errosSintaticos, tokensEsperados);
            }
        }else if (t.kind == START){
            try{BlocoStart(); BlocoEnd();} catch (ParseException f){
                errosSintaticosCount++;
                tokensEsperados = getListaTokensEsperados(e.expectedTokenSequences);
                TokenStringBuilder.formatErroSintaticoToString(e, errosSintaticos, tokensEsperados);
            }
        }else if (t.kind == DEFINE){
            try {BlocoDefine(); BlocoStart(); BlocoEnd();} catch (ParseException f){
                errosSintaticosCount++;
                tokensEsperados = getListaTokensEsperados(e.expectedTokenSequences);
                TokenStringBuilder.formatErroSintaticoToString(e, errosSintaticos, tokensEsperados);
            }
        }
    }
}

void NomeDoPrograma() :
{Token t;}
{
    (
    t=<IDENTIFIER> {executarAcaoSemantica(new Runnable() {@Override public void run() {analisadorSemantico.P1(t.image);}});}
    )?
}

void BlocoDefine() :
{}
{
    (
        <DEFINE> { executarAcaoSemantica(new Runnable() { public void run() { analisadorSemantico.D0();}});} ListaDeVariaveis()
    )?
}

void BlocoStart() :
{}
{
    try{
        <START> ListaDeComandos()
    }catch (ParseException e){
        if(isEOF()){throw e;}

        errosSintaticosCount++;
        String tokensEsperados = getListaTokensEsperados(e.expectedTokenSequences);

        TokenStringBuilder.formatErroSintaticoToString(e, errosSintaticos, tokensEsperados);

        if (tokensEsperados.contains("start")){
            errosSintaticos.append("Erro sintático: Seção de comandos deve iniciar com 'START'").append("\n\n");
        }

        skipToSynchronizingToken(SET, READ, SHOW, IF, LOOP, END);
        Token t = getToken(1);
        if (t.kind != END){ // se achar END, desempilha
            try{ListaDeComandos();} catch (ParseException f){ // melhorar
                errosSintaticosCount++;
                tokensEsperados = getListaTokensEsperados(e.expectedTokenSequences);
                TokenStringBuilder.formatErroSintaticoToString(e, errosSintaticos, tokensEsperados);
            }
        }
    }
}

void BlocoEnd() :
{}
{
    try{
        <END> <DOT> { executarAcaoSemantica(new Runnable() { public void run() { analisadorSemantico.P2();}});}
    }catch (ParseException e){
        errosSintaticosCount++;
        String tokensEsperados = getListaTokensEsperados(e.expectedTokenSequences);

        TokenStringBuilder.formatErroSintaticoToString(e, errosSintaticos, tokensEsperados);

        if (tokensEsperados.contains("end") || tokensEsperados.contains("DOT")) {
            errosSintaticos.append("Erro sintático: Programa deve acabar com 'END.'").append("\n\n");
        }
    }
}

// DECLARACOES

void ListaDeVariaveis() :
{}
{
    Variavel() ListaDeVariaveisAux()
}

void ListaDeVariaveisAux() :
{}
{
    (
        Variavel() ListaDeVariaveisAux()
    )?
}

void Variavel() :
{}
{
    try{
        ListaIdentificadores() <COLON> Tipo() DeclaracaoArray() <SEMICOLON> { executarAcaoSemantica(new Runnable() { public void run() { analisadorSemantico.D6();}});} // erro aqui?
    }catch (ParseException e){
        if(isEOF()){throw e;}

        Token t = getToken(1);
        if(t.kind != START && t.kind != IDENTIFIER && t.kind != EOF){
            errosSintaticosCount++;
            String tokensEsperados = getListaTokensEsperados(e.expectedTokenSequences);

            TokenStringBuilder.formatErroSintaticoToString(e, errosSintaticos, tokensEsperados);

            if(tokensEsperados.contains(":")){
                 errosSintaticos.append("Erro sintático: Declaração de variável deve conter ':' após a lista de variáveis").append("\n\n");
            }else if(tokensEsperados.contains(";")){
                errosSintaticos.append("Erro sintático: Declaração da variável deve terminar com ';' após especificação do tipo").append("\n\n");
            }
            skipToSynchronizingToken(IDENTIFIER, START);
        }
    }
}

void ListaIdentificadores() :
{Token t;}
{
    try{
        t=<IDENTIFIER> { executarAcaoSemantica(new Runnable() { public void run() { analisadorSemantico.D1(t.image);}});} ListaIdentificadoresAux()
    }catch (ParseException e){
        if(isEOF()){throw e;}

        errosSintaticosCount++;
        String tokensEsperados = getListaTokensEsperados(e.expectedTokenSequences);

        TokenStringBuilder.formatErroSintaticoToString(e, errosSintaticos, tokensEsperados);
        errosSintaticos.append("Erro sintático: O bloco 'Define' deve conter ao menos uma declaração de variável (identificador : tipo ;)").append("\n\n");

        skipToSynchronizingToken(START, COLON, IDENTIFIER);
    }
}

void ListaIdentificadoresAux() :
{Token t;}
{
    try{
        (
            <COMMA> t=<IDENTIFIER> { executarAcaoSemantica(new Runnable() { public void run() { analisadorSemantico.D1(t.image);}});} ListaIdentificadoresAux()
        )?
    }catch (ParseException e){
        if(isEOF()){throw e;}

        errosSintaticosCount++;
        String tokensEsperados = getListaTokensEsperados(e.expectedTokenSequences);

        TokenStringBuilder.formatErroSintaticoToString(e, errosSintaticos, tokensEsperados);

        if(tokensEsperados.contains(",")){
             errosSintaticos.append("Erro sintático: Elementos declarados devem estar separados por ','").append("\n\n");
        }else if(tokensEsperados.contains("identificador")){
            errosSintaticos.append("Erro sintático: Deve conter ao menos um 'Identificador' após a ','").append("\n\n");
        }
        skipToSynchronizingToken(COMMA, START, COLON, IDENTIFIER);

        Token p = getToken(1);
        if(p.kind == COMMA){
            ListaIdentificadoresAux();
        }
    }
}

void Tipo() :
{}
{
    try{
         <TYPE_NUM> { executarAcaoSemantica(new Runnable() { public void run() { analisadorSemantico.T(1); } }); }
        | <TYPE_REAL> { executarAcaoSemantica(new Runnable() { public void run() { analisadorSemantico.T(2); } }); }
        | <TYPE_TEXT> { executarAcaoSemantica(new Runnable() { public void run() { analisadorSemantico.T(3); } }); }
        | <TYPE_FLAG> { executarAcaoSemantica(new Runnable() { public void run() { analisadorSemantico.T(4); } }); }
    }catch (ParseException e){
        if(isEOF()){throw e;}

        errosSintaticosCount++;
        String tokensEsperados = getListaTokensEsperados(e.expectedTokenSequences);

        TokenStringBuilder.formatErroSintaticoToString(e, errosSintaticos, tokensEsperados);

        if(tokensEsperados.contains("numero inteiro") || tokensEsperados.contains("numero real") || tokensEsperados.contains("texto") || tokensEsperados.contains("constante logica")){
             errosSintaticos.append("Erro sintático: Tipo declarado deve pertencer ao conjunto: {'NUM', 'REAL', 'TEXT', 'TRUE', 'FALSE'}").append("\n\n");
        }
        Token p = skipToSynchronizingToken(LBRACKET, START, IDENTIFIER);

        if(p.kind == IDENTIFIER | p.kind == START){
            throw new ParseException("Sincronizado com identificador ou start dentro de Tipo()");
        }
    }
}

void DeclaracaoArray() :
{Token t;}
{
    try{
        <LBRACKET> t=<NUMBER> { executarAcaoSemantica(new Runnable() { public void run() { analisadorSemantico.V1(Integer.parseInt(t.image)); } }); } <RBRACKET> DeclaracaoValoresArray() { executarAcaoSemantica(new Runnable() { public void run() { analisadorSemantico.V2(); } }); }
        | DeclaracaoValoresEscalar() { executarAcaoSemantica(new Runnable() { public void run() { analisadorSemantico.E2(); } }); }
    }catch(ParseException e){
        if(isEOF()){throw e;}

        errosSintaticosCount++;
        String tokensEsperados = getListaTokensEsperados(e.expectedTokenSequences);

        TokenStringBuilder.formatErroSintaticoToString(e, errosSintaticos, tokensEsperados);

        if(tokensEsperados.contains("[") || tokensEsperados.contains("]")){
            errosSintaticos.append("Erro sintático: Declaração de tamanho do array deve ser feita entre '[' e ']'").append("\n\n");
        }
        // else aqui para <number>?
        skipToSynchronizingToken(IDENTIFIER, START, ASSIGN);

        Token p = getToken(1);
        if(p.kind == ASSIGN) {
            try{
                DeclaracaoValoresArray();
                executarAcaoSemantica(new Runnable() { public void run() { analisadorSemantico.V2(); } });
            }catch (ParseException f){ //melhorar
                if(isEOF()){throw f;}
                errosSintaticosCount++;
                tokensEsperados = getListaTokensEsperados(e.expectedTokenSequences);
                TokenStringBuilder.formatErroSintaticoToString(e, errosSintaticos, tokensEsperados);
            }
        }
    }
}

void DeclaracaoValoresArray() :
{ ArrayList<Object> listaValores = new ArrayList<>(); }
{
    try{
    ( // SUS - tive que transformar em final
        <ASSIGN> <LBRACE> { analisadorSemantico.setContexto("lista completa de vetor"); } listaValores=ListaDeValoresVetor() <RBRACE> {ArrayList<Object> finalListaValores = listaValores;  executarAcaoSemantica(new Runnable() { public void run() { analisadorSemantico.IV(finalListaValores); } }); analisadorSemantico.setContexto(""); }
    )?
    }catch(ParseException e){
        if(isEOF()){throw e;}

        errosSintaticosCount++;
        String tokensEsperados = getListaTokensEsperados(e.expectedTokenSequences);

        TokenStringBuilder.formatErroSintaticoToString(e, errosSintaticos, tokensEsperados);

        if(tokensEsperados.contains("=")){
            errosSintaticos.append("Erro sintático: Atribuições devem ser feitas a partir do simbolo '='").append("\n\n");
        }
        if(tokensEsperados.contains("{") || tokensEsperados.contains("}")){
            errosSintaticos.append("Erro sintático: Declarações de valores dentro de arrays devem ser feitas entre '{' e '}'").append("\n\n");
        }
        skipToSynchronizingToken(START, IDENTIFIER);
    }
}

void DeclaracaoValoresEscalar() :
{}
{
    (
        <ASSIGN> Valor() { executarAcaoSemantica(new Runnable() { public void run() { analisadorSemantico.IE(); } }); }
    )?
}

ArrayList<Object> ListaDeValoresVetor() :
{ ArrayList<Object> lista = new ArrayList<>(); Object val; }
{
    val = Valor() {lista.add(val); } ListaDeValoresVetorAux(lista)
    {return (lista);} // SUS - retorna lista ou val?
}

void ListaDeValoresVetorAux(ArrayList<Object> lista) :
{Object val;}
{
    (
        <COMMA> val=Valor() { lista.add(val); }  ListaDeValoresVetorAux(lista)
    )?
}

Object Valor() :
{Token t; Object value = null;}
{
    try{ // SUS - tive que transformar em final tambem
       t=<NUMBER> { value = Integer.parseInt(t.image); final Object finalValue = value;  executarAcaoSemantica(new Runnable() { public void run() { analisadorSemantico.C1((Integer)finalValue); } }); }
      | t=<REAL> { value = Float.parseFloat(t.image); final Object finalValue = value;  executarAcaoSemantica(new Runnable() { public void run() { analisadorSemantico.C2((Float)finalValue); } }); }
      | t=<TEXT_DOUBLE> { value = t.image.substring(1, t.image.length() - 1); final Object finalValue = value;  executarAcaoSemantica(new Runnable() { public void run() { analisadorSemantico.C3((String)finalValue); } }); }
      | t=<TEXT_SINGLE> { value = t.image.substring(1, t.image.length() - 1); final Object finalValue = value; executarAcaoSemantica(new Runnable() { public void run() { analisadorSemantico.C3((String)finalValue); } }); }  // constante literal com "" e com ''
      | t=<TRUE> { value = true; executarAcaoSemantica(new Runnable() { public void run() { analisadorSemantico.C4(); } }); }
      | t=<FALSE> { value = false; executarAcaoSemantica(new Runnable() { public void run() { analisadorSemantico.C5(); } }); }
    }catch (ParseException e){
        if(isEOF()){throw e;}

        errosSintaticosCount++;
        String tokensEsperados = getListaTokensEsperados(e.expectedTokenSequences);

        TokenStringBuilder.formatErroSintaticoToString(e, errosSintaticos, tokensEsperados);

        if (tokensEsperados.contains("numero inteiro") || tokensEsperados.contains("numero real") || tokensEsperados.contains("texto")
           || tokensEsperados.contains("constante logica"))
        {
            errosSintaticos.append("Erro sintático: Elemento deve pertencer ao conjunto: {'Constante numérica', 'Constante numérica real', 'Constante literal', 'TRUE' ou 'FALSE'}").append("\n\n");
        }
        skipToSynchronizingToken(COMMA, START, IDENTIFIER);
    }
    {return(value);}
}

// COMANDOS

void ListaDeComandos() :
{}
{
    Comando() ListaDeComandosAux()
}

void ListaDeComandosAux() :
{}
{
    (
        Comando() ListaDeComandosAux()
    )?
}

void Comando() :
{}
{
        Atribuicao()
      | EntradaDeDados()
      | SaidaDeDados()
      | Selecao()
      | Repeticao()
}

void Atribuicao() :
{Token t;}
{
    try{
        <SET> t=<IDENTIFIER> { executarAcaoSemantica(new Runnable() { public void run() { analisadorSemantico.A1(t.image); } }); } Indice() { executarAcaoSemantica(new Runnable() { public void run() { analisadorSemantico.A2(); } }); } <ASSIGN> Expressao() { executarAcaoSemantica(new Runnable() { public void run() { analisadorSemantico.A3(); } }); } <SEMICOLON>
    }catch(ParseException e){
        if(isEOF()){throw e;}

        errosSintaticosCount++;
        String tokensEsperados = getListaTokensEsperados(e.expectedTokenSequences);

        TokenStringBuilder.formatErroSintaticoToString(e, errosSintaticos, tokensEsperados);

        errosSintaticos.append("Erro sintático: Comando de atribuição deve seguir a sintaxe: 'set identificador[indice]/identificador = (valor ou expressão);'").append("\n\n");

        skipToSynchronizingToken(SET, READ, SHOW, IF, LOOP);

    }
}

void Indice() :
{}
{
    try{
    (
        <LBRACKET> Expressao()  { executarAcaoSemantica(new Runnable() { public void run() { analisadorSemantico.I1(); } }); } <RBRACKET>
    )?
    }catch(ParseException e){
        if(isEOF()){throw e;}

        errosSintaticosCount++;
        String tokensEsperados = getListaTokensEsperados(e.expectedTokenSequences);

        TokenStringBuilder.formatErroSintaticoToString(e, errosSintaticos, tokensEsperados);

        errosSintaticos.append("Erro sintático: Indice deve estar entre '[' e ']'").append("\n\n");

        skipToSynchronizingToken(SET, READ, SHOW, IF, LOOP);
    }
}

void EntradaDeDados() :
{Token t;}
{
    try{
        <READ> <LPAREN> t=<IDENTIFIER> { executarAcaoSemantica(new Runnable() { public void run() { analisadorSemantico.R1(t.image); } }); } Indice() { executarAcaoSemantica(new Runnable() { public void run() { analisadorSemantico.R2(); } }); } <RPAREN> <SEMICOLON>
    }catch(ParseException e){
        if(isEOF()){throw e;}
        errosSintaticosCount++;

        String tokensEsperados = getListaTokensEsperados(e.expectedTokenSequences);

        TokenStringBuilder.formatErroSintaticoToString(e, errosSintaticos, tokensEsperados);

        errosSintaticos.append("Erro sintático: Comando de entrada de dados deve seguir a sintaxe: 'READ(identificador/identificador[indice]);'").append("\n\n");

        skipToSynchronizingToken(SET, READ, SHOW, IF, LOOP);
    }
}

void SaidaDeDados() :
{}
{
    try{
        <SHOW> <LPAREN> ListaDeSaida() <RPAREN> <SEMICOLON>
    }catch(ParseException e){
        if(isEOF()){throw e;}
        errosSintaticosCount++;

        String tokensEsperados = getListaTokensEsperados(e.expectedTokenSequences);

        TokenStringBuilder.formatErroSintaticoToString(e, errosSintaticos, tokensEsperados);

        errosSintaticos.append("Erro Sintatico: Comando de saida de dados deve seguir a sintaxe: 'SHOW(identificador/identificador[indice]);' ou\n\t 'SHOW(valor/valor,valor)'").append("\n\n");

        skipToSynchronizingToken(SET, READ, SHOW, IF, LOOP);
    }
}

void ListaDeSaida() :
{}
{
    Item() ListaDeSaidaAux()
}

void ListaDeSaidaAux() :
{}
{
    (
        <COMMA> Item() ListaDeSaidaAux()
    )?
}

void Item() :
{Token t;}
{
    try{
        t=<IDENTIFIER>  { executarAcaoSemantica(new Runnable() { public void run() { analisadorSemantico.S2(t.image); } }); } Indice() { executarAcaoSemantica(new Runnable() { public void run() { analisadorSemantico.S3(); } }); }
        | t=<NUMBER> { executarAcaoSemantica(new Runnable() { public void run() { analisadorSemantico.K1(Integer.parseInt(t.image)); } }); }
        | t=<REAL> { executarAcaoSemantica(new Runnable() { public void run() { analisadorSemantico.K2(Float.parseFloat(t.image)); } }); }
        | t=<TEXT_DOUBLE> { executarAcaoSemantica(new Runnable() { public void run() { analisadorSemantico.K3(t.image.substring(1, t.image.length() - 1)); } }); } | t=<TEXT_SINGLE> { executarAcaoSemantica(new Runnable() { public void run() { analisadorSemantico.K3(t.image.substring(1, t.image.length() - 1)); } }); }
    }catch(ParseException e){
        if(isEOF()){throw e;}
        errosSintaticosCount++;

        String tokensEsperados = getListaTokensEsperados(e.expectedTokenSequences);

        TokenStringBuilder.formatErroSintaticoToString(e, errosSintaticos, tokensEsperados);

        skipToSynchronizingToken(SET, READ, SHOW, IF, LOOP, COMMA, RPAREN);
    }
}

void Selecao() :
{boolean houveElse = false;}
{
    try{
        <IF> Expressao() { executarAcaoSemantica(new Runnable() { public void run() { analisadorSemantico.F1(); } }); } <THEN> ListaDeComandos() Else() { executarAcaoSemantica(new Runnable() { public void run() { analisadorSemantico.F3(houveElse); } }); } <END> <SEMICOLON>
    }catch(ParseException e){
        if(isEOF()){throw e;}
        errosSintaticosCount++;

        String tokensEsperados = getListaTokensEsperados(e.expectedTokenSequences);

        TokenStringBuilder.formatErroSintaticoToString(e, errosSintaticos, tokensEsperados);

        errosSintaticos.append("Erro sintático: Comando de seleção deve seguir a sintaxe: 'IF <condição> THEN <comandos> END'").append("\n\n");

        skipToSynchronizingToken(SET, READ, SHOW, IF, LOOP);
    }
}

void Else() :
{}
{
    (
        <ELSE> { executarAcaoSemantica(new Runnable() { public void run() { analisadorSemantico.F2(); } }); } ListaDeComandos()
    )?
}

void Repeticao() :
{}
{
    try{
        <LOOP> <WHILE> Expressao() { executarAcaoSemantica(new Runnable() { public void run() { analisadorSemantico.L1(); } }); } ListaDeComandos() { executarAcaoSemantica(new Runnable() { public void run() { analisadorSemantico.L2(); } }); } <END> <SEMICOLON> // SUS aqui
    }catch(ParseException e){
        if(isEOF()){throw e;}
        errosSintaticosCount++;

        String tokensEsperados = getListaTokensEsperados(e.expectedTokenSequences);

        TokenStringBuilder.formatErroSintaticoToString(e, errosSintaticos, tokensEsperados);

        if(tokensEsperados.contains("loop") || tokensEsperados.contains("while") || tokensEsperados.contains("end")){
            errosSintaticos.append("Erro sintático: Comando de repetição devem a sintaxe: 'LOOP WHILE <comandos> END").append("\n\n");
        }
        skipToSynchronizingToken(SET, READ, SHOW, IF, LOOP);
    }
}

// EXPRESSAO

void Expressao() :
{}
{
    ExpressaoAritmeticaOuLogica() ExpressaoAux()
}

void ExpressaoAux() :
{}
{
    (
          <EQUAL> ExpressaoAritmeticaOuLogica() { executarAcaoSemantica(new Runnable() { public void run() { analisadorSemantico.Requals(); } }); }
        | <NOT_EQUAL> ExpressaoAritmeticaOuLogica() { executarAcaoSemantica(new Runnable() { public void run() { analisadorSemantico.RnotEqual(); } }); }
        | <SHIFT_LEFT> ExpressaoAritmeticaOuLogica() { executarAcaoSemantica(new Runnable() { public void run() { analisadorSemantico.Rsmaller(); } }); }
        | <SHIFT_RIGHT> ExpressaoAritmeticaOuLogica() { executarAcaoSemantica(new Runnable() { public void run() { analisadorSemantico.Rbigger(); } }); }
        | <SHIFT_LEFT_ASSIGN> ExpressaoAritmeticaOuLogica() { executarAcaoSemantica(new Runnable() { public void run() { analisadorSemantico.RsmallerEqual(); } }); }
        | <SHIFT_RIGHT_ASSIGN> ExpressaoAritmeticaOuLogica() { executarAcaoSemantica(new Runnable() { public void run() { analisadorSemantico.RbiggerEqual(); } }); }
    )?
}

void ExpressaoAritmeticaOuLogica() :
{}
{
    Termo2() MenorPrioridade()
}

void MenorPrioridade() :
{}
{
    (
          <PLUS> Termo2() { executarAcaoSemantica(new Runnable() { public void run() { analisadorSemantico.ADD(); } }); } MenorPrioridade()
        | <MINUS> Termo2() { executarAcaoSemantica(new Runnable() { public void run() { analisadorSemantico.SUB(); } }); } MenorPrioridade()
        | <PIPE> Termo2() { executarAcaoSemantica(new Runnable() { public void run() { analisadorSemantico.OR(); } });} MenorPrioridade()
    )?
}

void Termo2() :
{}
{
    Termo1() MediaPrioridade()
}

void MediaPrioridade() :
{}
{
    (
        <STAR> Termo1() { executarAcaoSemantica(new Runnable() { public void run() { analisadorSemantico.MUL(); } }); } MediaPrioridade()
      | <SLASH> Termo1() { executarAcaoSemantica(new Runnable() { public void run() { analisadorSemantico.DIV(); } }); } MediaPrioridade()
      | <PERCENT> Termo1() { executarAcaoSemantica(new Runnable() { public void run() { analisadorSemantico.MOD(); } }); } MediaPrioridade()
      | <DOUBLE_PERCENT> Termo1() { executarAcaoSemantica(new Runnable() { public void run() { analisadorSemantico.REM(); } }); } MediaPrioridade()
      | <AMPERSAND> Termo1() { executarAcaoSemantica(new Runnable() { public void run() { analisadorSemantico.AND(); } });} MediaPrioridade()
    )?
}

void Termo1() :
{}
{
    Elemento() MaiorPrioridade()
}

void MaiorPrioridade() :
{}
{
    (
        <POWER> Elemento() { executarAcaoSemantica(new Runnable() { public void run() { analisadorSemantico.POW(); } });} MaiorPrioridade()
    )?
}

void Elemento() :
{Token t;}
{
    try{
      t=<IDENTIFIER> { executarAcaoSemantica(new Runnable() { public void run() { analisadorSemantico.E1(t.image); } }); } Indice() { executarAcaoSemantica(new Runnable() { public void run() { analisadorSemantico.E3(); } }); }
      | t=<NUMBER> { executarAcaoSemantica(new Runnable() { public void run() { analisadorSemantico.C1(Integer.parseInt(t.image)); } });}
      | t=<REAL> { executarAcaoSemantica(new Runnable() { public void run() { analisadorSemantico.C2(Float.parseFloat(t.image)); } }); }
      | t=<TEXT_DOUBLE> { executarAcaoSemantica(new Runnable() { public void run() { analisadorSemantico.C3(t.image.substring(1, t.image.length() - 1)); } }); } | t=<TEXT_SINGLE> { executarAcaoSemantica(new Runnable() { public void run() { analisadorSemantico.C3(t.image.substring(1, t.image.length() - 1)); } }); }  // constante literal com "" e com ''
      | <TRUE> { executarAcaoSemantica(new Runnable() { public void run() { analisadorSemantico.C4(); } }); }
      | <FALSE> { executarAcaoSemantica(new Runnable() { public void run() { analisadorSemantico.C5(); } }); }
      | <LPAREN> Expressao() <RPAREN>
      | <EXCLAMATION> <LPAREN> Expressao() <RPAREN> { executarAcaoSemantica(new Runnable() { public void run() { analisadorSemantico.NOT(); } }); }
    }catch (ParseException e){
        if(isEOF()){throw e;}

        errosSintaticosCount++;
        String tokensEsperados = getListaTokensEsperados(e.expectedTokenSequences);

        TokenStringBuilder.formatErroSintaticoToString(e, errosSintaticos, tokensEsperados);

        if (tokensEsperados.contains("identificador") || tokensEsperados.contains("numero inteiro") || tokensEsperados.contains("numero real") || tokensEsperados.contains("texto")
           || tokensEsperados.contains("constante logica"))
        {
            errosSintaticos.append("Erro sintático: Elemento deve pertencer ao conjunto: {'Identificador', 'Constante numérica', 'Constante numérica real', 'Constante literal', 'TRUE' ou 'FALSE'}").append("\n\n");
        }else if (tokensEsperados.contains("(") || tokensEsperados.contains(")")){
            errosSintaticos.append("Erro sintático: Elemento deve estar entre '(' e ')'").append("\n\n");
        }else if (tokensEsperados.contains("!")){
            errosSintaticos.append("Erro sintático: Elemento deve estar entre '!(' e ')'").append("\n\n");
        }
        skipToSynchronizingToken(RPAREN, RBRACKET, SET, READ, SHOW, IF, LOOP, THEN, SEMICOLON);
    }
}