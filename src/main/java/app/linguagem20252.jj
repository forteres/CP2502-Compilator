options {
    STATIC = false;
}

PARSER_BEGIN(Linguagem20252)
package app;

import java.io.InputStream;
import java.io.FileInputStream;
import java.io.FileNotFoundException;
import java.util.ArrayList;
import java.util.List;
import java.util.Objects;

public class Linguagem20252 {

    public static java.util.ArrayList analisar(InputStream input) throws ParseException {
            Linguagem20252 lexer = new Linguagem20252(input);
            java.util.ArrayList tokens = new java.util.ArrayList();
            Token token;
            do {
                token = lexer.getNextToken();
                tokens.add(token);
            } while (token.kind != Linguagem20252Constants.EOF);

            return tokens;
        }

}


PARSER_END(Linguagem20252)



SKIP:
{
< BLANK_SPACE: " ">
| <END_OF_LINE: "\n">
| <NEW_LINE:"\r">
| <TABULATION:"\t">
}

SPECIAL_TOKEN :
{
    < SINGLE_LINE_COMMENT: "//" (~["\n","\r"])* ("\n"|"\r"|"\r\n")>
    | < BLOCK_COMMENT: "/*" (~["*"] | "*" (~["/"]))* "*/">
    
}

TOKEN: {
        // LITERAIS ------------------------------------------
         < MALFORMED_REAL: (["0"-"9"])((["0"-"9"])?){2}  "." (["0"-"9"])* (["a"-"z","A"-"Z","_"])+ (["0"-"9","a"-"z","A"-"Z","_"])* >
        | < REAL: (["0"-"9"])((["0"-"9"])?){2} "." (["0"-"9"])+>
        | < MALFORMED_NUMBER: ((["0"-"9"]) ((["0"-"9"] ["a"-"z","A"-"Z","_"]) | (["a"-"z","A"-"Z","_"] ["0"-"9"] | ["a"-"z","A"-"Z","_"])) (["0"-"9"])*)
            | (["0"-"9"]){3} (["0"-"9"] | ["a"-"z","A"-"Z","_"])+>
        | < NUMBER: (["0"-"9"])((["0"-"9"])?){2}>
        | < TEXT_DOUBLE: "\"" ( (~["\"","\\","\n","\r"]) | ("\\" ~[]) )* "\"" >
        | < TEXT_SINGLE: "'"  ( (~["'","\\","\n","\r"])  | ("\\" ~[]) )* "'" >
        | < UNCLOSED_STRING: ("\"" | "\'") ( (~["\"", "'","\\","\n","\r"]) | ("\\" ~[]) )+ >
        | < UNCLOSED_QUOTE: ("\"" | "\'") >
        | < UNCLOSED_BLOCK_COMMENT: "/*" (~["*"] | ("*" ~["/"]))*  >
        // SIMBOLOS ESPECIAIS --------------------------------

        | < ASSIGN: "=" >
        | < EQUAL: "==" >
        | < NOT_EQUAL: "!=" >
        | < SEMICOLON: ";" >
        | < COLON: ":" >
        | < COMMA: "," >
        | < DOT: "." >
        | < LBRACE: "{" >
        | < RBRACE: "}" >
        | < LBRACKET: "[" >
        | < RBRACKET: "]" >
        | < LPAREN: "(" >
        | < RPAREN: ")" >
        | < PLUS: "+" >
        | < MINUS: "-" >
        | < STAR: "*" >
        | < SLASH: "/" >
        | < PERCENT: "%" >
        | < POWER: "**" >
        | < DOUBLE_PERCENT: "%%" >
        | < SHIFT_LEFT: "<<" >
        | < SHIFT_RIGHT: ">>" >
        | < SHIFT_LEFT_ASSIGN: "<<=" >
        | < SHIFT_RIGHT_ASSIGN: ">>=" >
        | < EXCLAMATION: "!" >
        | < PIPE: "|" >
        | < AMPERSAND: "&" >

        // TIPOS PRIMITIVOS ----------------------------------

        | < TYPE_NUM:  (("n"|"N")("u"|"U")("m"|"M")) >
        | < TYPE_REAL: (("r"|"R")("e"|"E")("a"|"A")("l"|"L")) >
        | < TYPE_TEXT: (("t"|"T")("e"|"E")("x"|"X")("t"|"T")) >
        | < TYPE_FLAG: (("f"|"F")("l"|"L")("a"|"A")("g"|"G")) >

        // PALAVRAS RESERVADAS -----------------------------------
        | < BEGIN:   (("b"|"B")("e"|"E")("g"|"G")("i"|"I")("n"|"N")) >
        | < DEFINE:  (("d"|"D")("e"|"E")("f"|"F")("i"|"I")("n"|"N")("e"|"E")) >
        | < START:   (("s"|"S")("t"|"T")("a"|"A")("r"|"R")("t"|"T")) >
        | < END:     (("e"|"E")("n"|"N")("d"|"D")) >
        | < SET:     (("s"|"S")("e"|"E")("t"|"T")) >
        | < READ:    (("r"|"R")("e"|"E")("a"|"A")("d"|"D")) >
        | < SHOW:    (("s"|"S")("h"|"H")("o"|"O")("w"|"W")) >
        | < IF:      (("i"|"I")("f"|"F")) >
        | < THEN:    (("t"|"T")("h"|"H")("e"|"E")("n"|"N")) >
        | < ELSE:    (("e"|"E")("l"|"L")("s"|"S")("e"|"E")) >
        | < LOOP:    (("l"|"L")("o"|"O")("o"|"O")("p"|"P")) >
        | < WHILE:   (("w"|"W")("h"|"H")("i"|"I")("l"|"L")("e"|"E")) >

        // FLAG --------------------------------------------------
        | < TRUE:  (("t"|"T")("r"|"R")("u"|"U")("e"|"E")) >
        | < FALSE: (("f"|"F")("a"|"A")("l"|"L")("s"|"S")("e"|"E")) >

        // IDENTIFICADOR -------------------------------------
        | < MALFORMED_IDENTIFIER:
            (["a"-"z","A"-"Z","_"]) (((["a"-"z","A"-"Z","_"])* (["0"-"9"](["0"-"9"])+) (["a"-"z","A"-"Z","_"])*)+
            | (((["a"-"z","A"-"Z","_"])* (["0"-"9"] ["a"-"z","A"-"Z","_"])*)* ["0"-"9"]))
        >
        | < IDENTIFIER: ["a"-"z","A"-"Z","_"] ((["a"-"z","A"-"Z","_"])* (["0"-"9"] ["a"-"z","A"-"Z","_"])*)* >

        // ERRO ----------------------------------------------

        | < INVALID_SIMBOL: ~[]>
}

void Programa() :
{}
{
    <BEGIN> NomeDoPrograma() BlocoDefine() <START> ListaDeComandos() <END> <DOT>
}

void NomeDoPrograma() :
{}
{
    (<IDENTIFIER>)?
}

void Expressao() :
{}
{
    ExpressaoAritmeticaOuLogica() ExpressaoAux()
}

void ExpressaoAux() :
{}
{
    (
        <EQUAL> ExpressaoAritmeticaOuLogica()
      | <NOT_EQUAL> ExpressaoAritmeticaOuLogica()
      | <SHIFT_LEFT> ExpressaoAritmeticaOuLogica()
      | <SHIFT_RIGHT> ExpressaoAritmeticaOuLogica()
      | <SHIFT_LEFT_ASSIGN> ExpressaoAritmeticaOuLogica()
      | <SHIFT_RIGHT_ASSIGN> ExpressaoAritmeticaOuLogica()
    )?
}

void ExpressaoAritmeticaOuLogica() :
{}
{
    Termo2() MenorPrioridade()
}

void MenorPrioridade() :
{}
{
    (
        <PLUS> Termo2() MenorPrioridade()
      | <MINUS> Termo2() MenorPrioridade()
      | <PIPE> Termo2() MenorPrioridade()
    )?
}

void Termo2() :
{}
{
    Termo1() MediaPrioridade()
}

void MediaPrioridade() :
{}
{
    (
        <STAR> Termo1() MediaPrioridade()
      | <SLASH> Termo1() MediaPrioridade()
      | <PERCENT> Termo1() MediaPrioridade()
      | <DOUBLE_PERCENT> Termo1() MediaPrioridade()
      | <AMPERSAND> Termo1() MediaPrioridade()
    )?
}

void Termo1() :
{}
{
    Elemento() MaiorPrioridade()
}

void MaiorPrioridade() :
{}
{
    (
        <POWER> Elemento() MaiorPrioridade()
    )?
}

void Elemento() :
{}
{

        <IDENTIFIER>
      | <NUMBER>
      | <REAL>
      | <TEXT_DOUBLE> | <TEXT_SINGLE>  // constante literal com "" e com ''
      | <TRUE>
      | <FALSE>
      | <LPAREN> Expressao() <RPAREN>
      | <EXCLAMATION> <LPAREN> Expressao() <RPAREN>

}

void ListaDeElementos() :
{}
{
    Elemento() ListaDeElementosAux()
}

void ListaDeElementosAux() :
{}
{
    (
        <COMMA> ListaDeElementos()
    )?
}

void ListaIdentificadores() :
{}
{
    <IDENTIFIER> ListaIdentificadoresAux()
}

void ListaIdentificadoresAux() :
{}
{
    (
        <COMMA> <IDENTIFIER> ListaIdentificadores()
    )?
}

void Variavel() :
{}
{
    ListaIdentificadores() <COLON> DeclaracaoTipo() <SEMICOLON>
}

void Tipo() :
{}
{

        <TYPE_NUM>
      | <TYPE_REAL>
      | <TYPE_TEXT>
      | <TYPE_FLAG>

}

void DeclaracaoTipo() :
{}
{
    Tipo() SufixoDoTipo()
}

void SufixoDoTipo() :
{}
{
    (
        <ASSIGN> Elemento()
      | DeclaracaoArray()
    )?
}

void DeclaracaoArray() :
{}
{
    <LBRACKET> <NUMBER> <RBRACKET> DeclaracaoValoresArray()
}

void DeclaracaoValoresArray() :
{}
{
    (
        <ASSIGN> <LBRACE> ListaDeElementos() <RBRACE>
    )?
}

void ListaDeVariaveis() :
{}
{
    Variavel() ListaDeVariaveisAux()
}

void ListaDeVariaveisAux() :
{}
{
    (
        ListaDeVariaveis()
    )?
}

void BlocoDefine() :
{}
{
    (
        <DEFINE> ListaDeVariaveis()
    )?
}

void Atribuicao() :
{}
{
    <SET> <IDENTIFIER> SufixoDaAtribuicao() <SEMICOLON>
}

void SufixoDaAtribuicao() :
{}
{

        <ASSIGN> Expressao()
      | <LBRACKET> Expressao() <RBRACKET> <ASSIGN> Expressao()

}

void EntradaDeDados() :
{}
{
    <READ> <LPAREN> <IDENTIFIER> SufixoIdentificador() <RPAREN> <SEMICOLON>
}

void SufixoIdentificador() :
{}
{
    (
        <LBRACKET> Expressao() <RBRACKET>
    )?
}

void SaidaDeDados() :
{}
{
    <SHOW> <LPAREN> ListaDeElementos() <RPAREN> <SEMICOLON>
}

void Selecao() :
{}
{
    <IF> Expressao() <THEN> ListaDeComandos() Else() <END> <SEMICOLON>
}

void Else() :
{}
{
    (
        <ELSE> ListaDeComandos()
    )?
}

void Repeticao() :
{}
{
    <LOOP> <WHILE> Expressao() ListaDeComandos() <END> <SEMICOLON>
}

void Comando() :
{}
{

        Atribuicao()
      | EntradaDeDados()
      | SaidaDeDados()
      | Selecao()
      | Repeticao()

}

void ListaDeComandos() :
{}
{
    Comando() ListaDeComandosAux()
}

void ListaDeComandosAux() :
{}
{
    (
        ListaDeComandos()
    )?
}
